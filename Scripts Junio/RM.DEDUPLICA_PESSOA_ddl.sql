------- Generated by SYS.DBMS_METADATA on 28-jan-2010 at 14:22:07 -------

  CREATE OR REPLACE PROCEDURE "RM"."DEDUPLICA_PESSOA" ( LIMIAR FLOAT ) IS
--
-- Purpose: IDENTIFICAR TODOS OS REGISTROS DUPLICADOS NA TABELA PPESSOA
--
CURSOR CTOKEN IS 
   SELECT GET_TOKEN(NOME,1)NOMEA, GET_TOKEN(NOME,2)NOMEB FROM TOKEN WHERE QTD>1 ORDER BY QTD DESC;

CURSOR CPESSOA(PNOMEA VARCHAR2, PNOMEB VARCHAR2) IS
       SELECT CODIGO, NOME--, 
              --TRIM(UPPER(TRIM(NOME)||' '||TRIM(dtnascimento)||' '||TRIM(RUA)||' '|| TRIM(NUMERO)||' '||
              --TRIM(BAIRRO) ||' '||TRIM(CIDADE) ||' '|| TRIM(CEP) ||' '||TRIM(TELEFONE1))) REG
       FROM PPESSOA 
       WHERE TRANSLATE(TRIM(UPPER(NOME)),'ÁÃÂÀÉÊÈÍÌÓÒÔÕÚÙÛ','AAAAEEEIIOOOOUUU') LIKE '%'||PNOMEA||'%'
         AND TRANSLATE(TRIM(UPPER(NOME)),'ÁÃÂÀÉÊÈÍÌÓÒÔÕÚÙÛ','AAAAEEEIIOOOOUUU') LIKE '%'||PNOMEB||'%'
         AND CPF  IS NULL
         AND CARTIDENTIDADE IS NULL;
         
TYPE TPESSOA IS TABLE OF CPESSOA%ROWTYPE;
RPESSOA TPESSOA;
RPESSOA_NULL TPESSOA;
I INTEGER;
J INTEGER;
SIM FLOAT:=0;
V CHAR(1);

BEGIN
   DBMS_OUTPUT.Put_Line('INICIO - DEDUPLICAO DA TABELA RM!');  
   DELETE FROM DEDUPRES;COMMIT;
   DBMS_OUTPUT.Put('GERANDO TOKENS...');  
   gera_token2; -- GERA OS TOKENS DO CAMPO NOME DA TABELA PPESSOA
   DBMS_OUTPUT.Put_LINE('GERADOS COM SUCESSO!');  

   DBMS_OUTPUT.Put_Line('CALCULANDO SIMILARIDADE...!');  
   FOR X IN CTOKEN LOOP
      DBMS_OUTPUT.Put('PROCESSANDO TOKEN '''||X.NOMEA||' '||X.NOMEB||'''');

      OPEN CPESSOA(X.NOMEA,X.NOMEB);
      FETCH CPESSOA BULK COLLECT INTO RPESSOA;
      CLOSE CPESSOA;
      DBMS_OUTPUT.Put_Line(' QTD='||RPESSOA.COUNT);  
      IF RPESSOA.COUNT>1 THEN
         FOR I IN RPESSOA.FIRST..RPESSOA.LAST-1 LOOP        
            FOR J IN I+1..RPESSOA.LAST LOOP
               SIM := utl_match.JARO_WINKLER(RPESSOA(I).NOME,RPESSOA(J).NOME);
               IF(SIM>=LIMIAR)
                  THEN V := 'V';
                  ELSE V := 'F';
               END IF;

               BEGIN
                  -- INSERE NA TABELA DE RESULTADO
                  INSERT INTO dedupres(CODA,CODB,SIM,VOTE)VALUES(RPESSOA(I).CODIGO,RPESSOA(J).CODIGO,SIM,V);
                  COMMIT;
               EXCEPTION
                  WHEN DUP_VAL_ON_INDEX THEN NULL;
               END;
            END LOOP; -- J
         END LOOP; -- I
      END IF;
      RPESSOA := RPESSOA_NULL;
   END LOOP; -- X
   DBMS_OUTPUT.Put_Line('PROCESSO FINALIZADO...!');  
   
   GERA_RES_DEDUP; -- GERA O RESULTADO FINAL ATRAVES DE GRUPOS DE REGISTROS SIMILARES
   GERA_RES_DUP_CPF; -- GERA O RESULTADO FINAL ATRAVES DE GRUPOS DE REGISTROS SIMILARES

EXCEPTION
   WHEN OTHERS THEN
      DBMS_OUTPUT.PUT_LINE(' ');
      DBMS_OUTPUT.PUT_LINE(SQLERRM);     
      DBMS_OUTPUT.put_line(DBMS_UTILITY.format_error_backtrace);

END; -- Procedure 
 
 
/
