REM utildemo.sql
REM Version 1.0, last updated 7/19/97
REM This procedure demonstrates several of the HTML utilities in the OWA_UTIL
REM package, as described in Chapter 19 of _Oracle8 PL/SQL Programming_
REM by Scott Urman.

CREATE OR REPLACE PROCEDURE UtilDemo AS
  v_Temp BOOLEAN;
BEGIN
  HTP.htmlOpen;
  HTP.headOpen;
  HTP.title('OWA_UTIL HTML Demos');
  HTP.headClose;

  HTP.bodyOpen;
  HTP.p('This page demonstrates several of the HTML utilities ');
  HTP.p('in the OWA_UTIL package.' || HTF.para);

  HTP.header(2, 'print_cgi_env');
  HTP.p('The following is generated by ');
  HTP.bold('OWA_UTIL.print_cgi_env');
  HTP.p(':' || HTF.nl);
  OWA_UTIL.print_cgi_env;

  HTP.header(2, 'get_owa_service_path');
  HTP.p('The following is generated by ');
  HTP.bold('OWA_UTIL.get_owa_service_path');
  HTP.p(':' || HTF.nl);
  HTP.p(OWA_UTIL.get_owa_service_path);

  HTP.header(2, 'tableprint');
  HTP.p('The following table (containing information from ');
  HTP.p('registered_students) is generated by ');
  HTP.bold('OWA_UTIL.tableprint');
  HTP.p(':' || HTF.nl);
  v_Temp := OWA_UTIL.tableprint(
              ctable => 'registered_students',
              cattributes => 'BORDER=1',
              ntable_type => OWA_UTIL.html_table,
              cclauses => 'ORDER BY student_id');

  HTP.p('Here is the same data, as a preformatted table instead:');
  HTP.nl;
  v_Temp := OWA_UTIL.tableprint(
              ctable => 'registered_students',
              cattributes => 'BORDER=1',
              ntable_type => OWA_UTIL.pre_table,
              cclauses => 'ORDER BY student_id');

  OWA_UTIL.signature('UtilDemo');
  HTP.bodyClose;
END UtilDemo;
/
